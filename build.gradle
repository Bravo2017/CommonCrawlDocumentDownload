apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

sourceCompatibility = 1.7

repositories {
	mavenCentral()
}

dependencies {
    compile 'org.dstadler:commons-dost:1.0.0.+'
    compile 'org.apache.httpcomponents:httpclient:4.3.3'
    //compile 'org.netpreserve.commons:webarchive-commons:1.1.5'

	// for Little Endian number parsing
    compile 'org.apache.poi:poi:3.12-beta1'
    
    // for commoncrawl-source
    compile 'log4j:log4j:1.2.17'
    compile 'org.jsoup:jsoup:1.8.1'

	//compile 'commons-io:commons-io:2.4'
	//compile 'org.apache.commons:commons-lang3:3.4'

	//compile 'com.google.guava:guava:18.0'   

	testCompile "junit:junit:4+"
    testCompile 'org.dstadler:commons-test:1.0.0.+'
}

// work around unnecessary timestamp in generated file which always causes dirty files in version control
// https://issues.gradle.org/browse/GRADLE-2293
task adjustEclipseSettingsFile << {
   ant.replaceregexp(match:'^#.*', replace:'', flags:'g', byline:true) {
      fileset(dir: project.projectDir, includes: '.settings/org.eclipse.jdt.core.prefs')
   }
}
task sortEclipseSettingsFile << {
   new File(project.projectDir, '.settings/org.eclipse.jdt.core.prefs').with { it.text = it.readLines().findAll { it }.sort().join('\n') }
}
eclipseJdt.finalizedBy adjustEclipseSettingsFile
eclipseJdt.finalizedBy sortEclipseSettingsFile

test {
	systemProperties = System.properties

	// show standard out and standard error of the test JVM(s) on the console
	testLogging.showStandardStreams = true

	// http://forums.gradle.org/gradle/topics/jacoco_related_failure_in_multiproject_build
	systemProperties['user.dir'] = workingDir
}

jacoco {
	toolVersion = "0.7.4.201502262128"
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}


task download(type:JavaExec,dependsOn: compileJava) {
	description = 'Reading blocks at beginning or the last stored block-number and download binary data from the common crawl archives'
	
	main = 'org.dstadler.commoncrawl.ReadAndDownload'
	classpath = sourceSets.main.runtimeClasspath
}
